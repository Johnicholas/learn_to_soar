# These operators less-or-equal and less-than are used by the heap to compare the elements
# it contains. These specific productions compare things that are just numbers, below a ^test link,
# for testing purposes.

sp {test*less-or-equal*initialize
    (state <s> ^superstate.operator <o>)
    (<o> ^name less-or-equal ^left.test <n1> ^right.test <n2> ^return-to <r>)
-->
    (<s> ^name test-less-or-equal ^left <n1> ^right <n2> ^min (min <n1> <n2>) ^return-to <r>)}

sp {test-less-or-equal*equal
    (state <s> ^name test-less-or-equal ^left <n> ^right <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> true)}

sp {test-less-or-equal*less
    (state <s> ^name test-less-or-equal ^left <n> ^min <n> ^right <> <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> true)}

sp {test-less-or-equal*greater
    (state <s> ^name test-less-or-equal ^right <n> ^min <n> ^left <> <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> false)}

sp {test*less-than*initialize
    (state <s> ^superstate.operator <o>)
    (<o> ^name less-than ^left.test <n1> ^right.test <n2> ^return-to <r>)
-->
    (<s> ^name test-less-than ^left <n1> ^right <n2> ^min (min <n1> <n2>) ^return-to <r>)}

sp {test-less-than*equal
    (state <s> ^name test-less-than ^left <n> ^right <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> false)}

sp {test-less-than*less
    (state <s> ^name test-less-than ^left <n> ^min <n> ^right <> <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> true)}

sp {test-less-than*greater
    (state <s> ^name test-less-than ^right <n> ^min <n> ^left <> <n> ^superstate <ss> ^return-to <r>)
-->
    (<ss> ^<r> false)}



