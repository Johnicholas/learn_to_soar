# This is an SEC interpreter, and corresponding SEC compiler and vm from
# Ager et al's "From Interpreter to Compiler and Virtual Machine"

main(?term) == mainhelper(eval(Nil, Mt, ?term))
mainhelper(Pair(Cons(?val, Nil), ?ignored)) == ?val

eval(?s, ?e, Lit(?n)) == Pair(Cons(Int(?n), ?s), ?e)
eval(?s, ?e, Var(?x)) == Pair(Cons(lookup(?e, ?x), ?s), ?e)
lookup(Extend(?x, ?v, ?e), ?y) == if ?x = ?y then ?v else lookup(?e, ?y)
eval(?s, ?e, Lam(?x, ?t)) == Pair(Cons(Closure(?x, ?e, ?t), ?s), ?e)
eval(?s, ?e, App(?t0, ?t1)) == evalapphelper1(eval(?s, ?e, ?t1), ?t0)
evalapphelper1(Pair(?sprime, ?eprime), ?t0) ==
    evalapphelper2(eval(?sprime, ?eprime, ?t0))
evalapphelper2(Pair(Cons(Closure(?fx, ?fe, ?ft), Cons(?v, ?s)), ?e)) ==
    evalapphelper3(applyclosure(?fx, ?fe, ?ft, Cons(?v, Nil), ?e), ?s, ?e)
applyclosure(?fx, ?fe, ?ft, Cons(?v, ?sprime), ?eprime) ==
    eval(Nil, Extend(?fx, ?v, ?fe), ?ft)
evalapphelper3(Pair(Cons(?v, Nil), ?ignored), ?s, ?e) ==
    Pair(Cons(?v, ?s), ?e)
eval(?s, ?e, Succ(?t)) ==
    evalsucchelper(eval(?s, ?e, ?t))
evalsucchelper(Pair(Cons(Int(?n), ?s), ?e)) ==
    Pair(Cons(Int(?n + 1), ?s), ?e)

# test the interpreter less superficially:
# scott encoding for Nat = Z | S(nat)
Z == Lam(x, Lam(y, Var(x)))
S == Lam(pred, Lam(x, Lam(y, App(Var(y), Var(pred)))))
# this is the Z combinator, a fix-point combinator usable in call-by-value languages
fix == Lam(f, App(Lam(x, App(Var(f), Lam(y, App(App(Var(x), Var(x)), Var(y))))),
                  Lam(x, App(Var(f), Lam(y, App(App(Var(x), Var(x)), Var(y)))))))
# almost_plus total_plus Z y = y
# almost_plus total_plus S(pred) y = S(total_plus(pred, y)
almostplus == Lam(tot, Lam(x, Lam(y, App(App(Var(x), Var(y)), Lam(pred, App(S, App(App(Var(tot), Var(pred)), Var(y))))))))
# plus = App(fix, almostplus)
plus == App(fix, almostplus)
# We need a converter from scott-encoded nats to readable primitive numbers
# almosttoprimitive tot Z = Lit(0)
# almosttoprimitive tot S pred = Succ(App(Var(tot), Var(pred)))
almosttoprimitive == Lam(tot, Lam(x, App(App(Var(x), Lit(0)), Lam(pred, Succ(App(Var(tot), Var(pred)))))))
toprimitive == App(fix, almosttoprimitive)

# tests 
main(Succ(Succ(Lit(0)))) = Int(2)
main(App(toprimitive, Z)) = Int(0)
main(App(toprimitive, App(S, Z))) = Int(1)
# 2 + 2 = 4
# toprimitive(plus(S(S(Z)), S(S(Z)))) = Int(4)
main(App(toprimitive, App(App(plus, App(S, App(S, Z))), App(S, App(S, Z))))) = Int(4)

# almostfib tot Z = S(Z)
# almostfib tot S(pred) = fib2 tot pred
almostfib == Lam(tot, Lam(x, App(App(Var(x), App(S, Z)), Lam(pred, App(App(fib2, Var(tot)), Var(pred))))))
# fib2 tot Z = S(Z)
# fib2 tot S(pred) = plus(tot(pred), tot(S(pred)))
fib2 == Lam(tot, Lam(x, App(App(Var(x), App(S, Z)), Lam(pred, App(App(plus, App(Var(tot), Var(pred))), App(Var(tot), App(S, Var(pred))))))))
fib == App(fix, almostfib)

# fib 4 = 5
# toprimitive(fib(S(S(S(S(Z)))))) = Int(5)
main(App(toprimitive, App(fib, App(S, App(S, App(S, App(S, Z))))))) = Int(5)

# this compiler is also from Ager et al., but it doesn't handle Lit or Succ
# t denotes terms
# i denotes instructions
# c denotes lists of instructions
# e denotes environments
# v denotes expressible values
# s denotes stacks of expressible values
# t ::= Var(name) | Lam(name, t) | App(t, t)
# i ::= Access(name) | Close(name, c) | Call
# v ::= Val(name, c, e)

compile(Var(?x)) == Cons(Access(?x), Nil)
compile(Lam(?x, ?t)) == Cons(Close(?x, compile(?t)), Nil)
compile(App(?t0, ?t1)) == append(compile(?t1), append(compile(?t0), Cons(Call, Nil)))
append(Nil, ?ys) == ?ys
append(Cons(?x, ?xs), ?ys) == Cons(?x, append(?xs, ?ys))

# test the compiler superficially: (I K)
compile(App(Lam(x, Var(x)), Lam(x, Lam(y, Var(x)))))

# this machine is also from Ager et al.
# initial transition
machine_main(?c) == sec(Nil, Mt, ?c)
# state machine
sec(?s, ?e, Cons(Access(?x), ?c)) ==
    sec(Cons(lookup(?e, ?x), ?s), ?e, ?c)
sec(?s, ?e, Cons(Close(?x, ?cprime), ?c)) ==
    sec(Cons(Val(?x, ?cprime, ?e), ?s), ?e, ?c)
sec(Cons(Val(?x, ?cprime, ?eprime), Cons(?v, ?s)), ?e, Cons(Call, ?c)) ==
    sec(?s, Extend(?x, ?v, ?eprime), append(?cprime, ?c))
sec(Cons(?v, ?s), ?e, Nil) == ?v

# test the compiler and the machine superficially
machine_main(compile(App(Lam(x, Var(x)), Lam(x, Lam(y, Var(x))))))
